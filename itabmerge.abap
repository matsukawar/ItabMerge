REPORT ZITABMERGE.

" New abap or Legacy abap selection.
PARAMETERS NEW_ABAP TYPE FLAG AS CHECKBOX.


START-OF-SELECTION.
TYPES:
  BEGIN OF LINE,
    X1 TYPE I,
    X2 TYPE I,
    X3 TYPE I,
  END OF LINE,
  TYP_ITAB1 TYPE STANDARD TABLE OF LINE WITH DEFAULT KEY,


  BEGIN OF LINE2,
    X1 TYPE I,
    X2 TYPE I,
    X3 TYPE I,
    X4 TYPE C,
  END OF LINE2,
  TYP_ITAB2 TYPE STANDARD TABLE OF LINE2 WITH DEFAULT KEY,


  BEGIN OF LINE3,
    X3 TYPE I,
    X1 TYPE I,
  END OF LINE3.


DATA(ITAB1) = VALUE TYP_ITAB1(
               ( X1 = 1 X2 = 1 X3 = 1 )
               ( X1 = 2 X2 = 1 X3 = 2 )
               ( X1 = 1 X2 = 2 X3 = 3 ) ).


DATA(ITAB2) = VALUE TYP_ITAB2(
               ( X1 = 2 X2 = 6 X3 = 2 X4 = 'A' )
               ( X1 = 3 X2 = 1 X3 = 3 X4 = 'B' )
               ( X1 = 3 X2 = 2 X3 = 4 X4 = 'C' ) ).


IF NEW_ABAP = ABAP_OFF.
  " Legacy Code


  " 通常のキー無しのSTANDARD TABLE型
  TYPES TYP_ITAB3_LEG TYPE STANDARD TABLE OF LINE3.


  DATA ITAB3_LEG TYPE TYP_ITAB3_LEG.


  MOVE-CORRESPONDING ITAB1 TO ITAB3_LEG.
  MOVE-CORRESPONDING ITAB2 TO ITAB3_LEG KEEPING TARGET LINES.


  SORT ITAB3_LEG BY X1 X3.


  DELETE ADJACENT DUPLICATES FROM ITAB3_LEG COMPARING X1 X3.


  CL_DEMO_OUTPUT=>DISPLAY( ITAB3_LEG ).
ELSE.
  " ABAP7.4


  " 項目X1とX3で構成された復号主キーを保有したソートテーブル型
  TYPES TYP_ITAB3_NEW TYPE SORTED TABLE OF LINE3 WITH UNIQUE KEY PRIMARY_KEY COMPONENTS X1 X3.


  DATA(ITAB3_NEW) = CORRESPONDING TYP_ITAB3_NEW(
             BASE ( CORRESPONDING TYP_ITAB3_NEW( ITAB1 ) ) ITAB2 DISCARDING DUPLICATES ).


  CL_DEMO_OUTPUT=>DISPLAY( ITAB3_NEW ).
ENDIF.
